apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'NewApi'
    }
}

dependencies {
    compile 'com.github.wrdlbrnft:simple-tasks:0.2.0.7'
    compile 'com.android.support:support-v4:25.0.1'
}

final bintrayUser = hasProperty('bintray_user') ? property('bintray_user') : ''
final bintrayApiKey = hasProperty('bintray_api_key') ? property('bintray_api_key') : ''
final versionName = hasProperty('version_name') ? property('version_name') : '0.1.0.0'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'source'
}

task javadoc(type: Javadoc, dependsOn: 'assembleRelease') {
    source = android.sourceSets.main.java.srcDirs
    title = 'SimpleRest'
    options.links('http://docs.oracle.com/javase/7/docs/api/');
    options.linksOffline('http://d.android.com/reference', "${android.sdkDirectory}/docs/reference");
    classpath += files(android.libraryVariants.javaCompile.classpath.files)
    classpath += files(android.getBootClasspath())
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Bintray(MavenPublication) {
            groupId 'com.github.wrdlbrnft'
            artifactId 'simple-rest'
            version versionName
            artifact sourceJar
            artifact javadocJar
            artifact new File(buildDir, '/outputs/aar/SimpleRest-release.aar')
            
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                project.configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode 'groupId', it.group
                        dependencyNode.appendNode 'artifactId', it.name
                        dependencyNode.appendNode 'version', it.version
                        final dependencyArtifact = it.artifacts[0];
                        if(dependencyArtifact != null) {
                            if (dependencyArtifact.classifier != null) {
                                dependencyNode.appendNode 'classifier', dependencyArtifact.classifier
                            }
                            if (dependencyArtifact.extension != null) {
                                dependencyNode.appendNode 'type', dependencyArtifact.extension
                            }
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Bintray']
    pkg {
        repo = 'maven'
        name = 'SimpleRest'
        userOrg = bintrayUser
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Wrdlbrnft/SimpleRest'
        publicDownloadNumbers = true
        version {
            name = versionName
            released = new Date()
        }
    }
}
